/*--
 * Copyright 2019 Ren√© M. de Bloois
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
	id "java"
	id "jacoco"
	id "com.github.johnrengelman.shadow" version "5.0.0"
}

repositories {
	jcenter()
}

println "Java version: ${JavaVersion.current()}"
println "Java home: ${System.properties."java.home"}"
if (JavaVersion.current() != JavaVersion.VERSION_1_8) {
    throw new GradleException("This build must be run with java 8")
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

compileJava {
	options.encoding = "UTF-8"
	options.debug = true
}

compileTestJava {
	options.encoding = "UTF-8"
}

dependencies {
	implementation(
		files("lib/solidstack.jar"),
		"commons-cli:commons-cli:1.2",
		"org.apache.commons:commons-lang3:3.4",
	)
	compileOnly(
//		"org.apache.maven:maven-plugin-api:2.0.8",
		"org.apache.maven:maven-core:2.0.8",
		"org.codehaus.groovy:groovy:1.5.7",
		"org.apache.ant:ant:1.7.1"
	)
	testImplementation(
		"org.testng:testng:6.14.3",
		"org.assertj:assertj-core:3.12.1",
		"org.jmockit:jmockit:1.42",
		"org.apache.ant:ant-testutil:1.7.1",
		"junit:junit:4.7",
		"org.slf4j:slf4j-api:1.7.2",
		"org.apache.commons:commons-io:1.3.2",
		"org.springframework:spring-beans:3.0.5.RELEASE",
		"org.springframework:spring-context:3.0.5.RELEASE",
		"org.springframework:spring-jdbc:3.0.5.RELEASE",
		"org.hsqldb:hsqldb:2.3.2",
		"org.apache.derby:derby:10.6.1.0",
		"org.apache.maven:maven-core:2.0.8"
	)
}

task copyTestHome(type: Copy) {
	from "src/test/home"
    into "${buildDir}/testhome"
}

test {
	dependsOn copyTestHome
	useTestNG() {
		useDefaultListeners = true
//		suites "src/test/suite.xml"
	}
	workingDir = "${buildDir}/testhome"
	jvmArgs "-javaagent:${classpath.find{it.name.contains("jmockit")}.absolutePath}"
}

task coverage {
	dependsOn test, jacocoTestReport
}

shadowJar {
 	minimize()
	relocate "solidstack", "solidbase.solidstack"
	relocate "org.apache.commons", "solidbase.commons"
}

assemble.dependsOn shadowJar

// Run the Ant and Maven tests from build.xml
ant.importBuild "build.xml"
ant.lifecycleLogLevel = "INFO"
ant."build.home" = buildDir

// clean.dependsOn "ant-clean"

check.dependsOn "test-maven-plugin", "test-ant-task"
